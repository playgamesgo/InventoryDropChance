plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'me.playgamesgo'
version = '1.6.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url "https://jitpack.io"
    }
    maven {
        name = "codemc"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "extendedclip"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "okaeri"
        url = "https://storehouse.okaeri.eu/repository/maven-releases/"
    }
    maven {
        name = "mirageRepositoryReleases"
        url = uri("https://repo.mirage-play.com/releases")
    }
    maven {
        name = "matteodev"
        url = "https://maven.devs.beer"
    }
    maven {
        name = "panda-lang"
        url = "https://repo.panda-lang.org/releases"
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"

    implementation "com.github.simplix-softworks:simplixstorage:3.2.7"

    implementation "eu.okaeri:okaeri-configs-hjson:${project.okaeriConfigsVersion}"
    implementation "eu.okaeri:okaeri-configs-yaml-bukkit:${project.okaeriConfigsVersion}"
    implementation "eu.okaeri:okaeri-configs-serdes-commons:${project.okaeriConfigsVersion}"
    implementation "eu.okaeri:okaeri-configs-serdes-bukkit:${project.okaeriConfigsVersion}"

    compileOnly "me.playgamesgo:plugin-annotations-continued:${project.pluginAnnotationsVersion}"
    annotationProcessor "me.playgamesgo:plugin-annotations-continued:${project.pluginAnnotationsVersion}"

    implementation "dev.rollczi:litecommands-bukkit:${project.liteCommandsVersion}"

    compileOnly "org.projectlombok:lombok:${project.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"

    implementation "de.tr7zw:item-nbt-api:2.15.0"
    compileOnly "dev.lone:api-itemsadder:4.0.2-beta-release-11"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.register('replaceVarsInJava', Copy) {
    from 'src/main/java'
    into "$buildDir/tmp"

    filter { line -> line.replace('${version}', project.version.toString()) }
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-parameters")
}

compileJava {
    dependsOn replaceVarsInJava
    source = fileTree("$buildDir/tmp")
}

shadowJar {
    relocate 'de.leonhard.storage', 'me.playgamesgo.libs.simplixstorage'
    relocate 'de.tr7zw.changeme.nbtapi', 'me.playgamesgo.libs.nbtapi'
    minimize()
}

tasks {
    runServer {
        dependsOn("shadowJar")
        minecraftVersion("1.21.5")
    }
}